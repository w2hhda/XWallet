import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.library'

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 26
        versionCode 1
        versionName "1.0"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    task ndkBuild(type: Exec) {
        def rootDir = project.rootDir
        def localProperties = new File(rootDir, "local.properties")
        Properties properties = new Properties()
        localProperties.withInputStream { instr ->
            properties.load(instr)
        }
        def ndkDir = properties.getProperty('ndk.dir')

        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            commandLine ndkDir + '\\ndk-build.cmd',
                    'NDK_LIBS_OUT=main/jniLibs',
                    'NDK_PROJECT_PATH=' + rootDir + '\\coinslib\\src',
                    'APP_BUILD_SCRIPT=jni/Android.mk',
                    '-C',
                    file('src').absolutePath
        } else {
            commandLine ndkDir + '/ndk-build',
                    'NDK_LIBS_OUT=main/jniLibs',
                    'NDK_PROJECT_PATH=' + rootDir + '/coinslib/src',
                    'APP_BUILD_SCRIPT=jni/Android.mk',
                    '-C',
                    file('src').absolutePath
        }
    }

    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn ndkBuild
    }

}

dependencies {
    compile 'com.squareup.okhttp3:okhttp:3.8.0'
    compile 'org.web3j:core-android:2.2.1'
    compile 'me.dm7.barcodescanner:zxing:1.8.4'

    //start bitherj
    compile 'com.google.guava:guava:13.0.1@jar'
    compile 'org.slf4j:slf4j-api:1.7.7'
    compile 'com.madgag.spongycastle:core:1.51.0.0'
    compile 'org.apache.httpcomponents:httpclient:4.2.1@jar'
    compile 'org.apache.httpcomponents:httpmime:4.1@jar'
    compile 'org.apache.httpcomponents:httpcore:4.3.2@jar'
    compile 'com.google.code.findbugs:jsr305:2.0.1@jar'
    //end bitherj
}
